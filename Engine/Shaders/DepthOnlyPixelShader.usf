// Copyright 1998-2016 Epic Games, Inc. All Rights Reserved.

/*=============================================================================
	DepthOnlyPixelShader.hlsl: Depth-only pixel shader.
=============================================================================*/

#include "Common.usf"
#include "Material.usf"
#include "VertexFactory.usf"

void Main(
#if SINGLE_PASS_STEREO
	in uint InViewportIndex : SV_ViewportArrayIndex,
#endif
#if !MATERIALBLENDING_SOLID || OUTPUT_PIXEL_DEPTH_OFFSET
	FVertexFactoryInterpolantsVSToPS FactoryInterpolants,
	float4 PixelPosition : TEXCOORD6,
	in INPUT_POSITION_QUALIFIERS float4 SvPosition : SV_Position
	OPTIONAL_IsFrontFace
	OPTIONAL_OutDepthConservative,
#endif
	out float4 OutColor : SV_Target0
	)
{
#if !MATERIALBLENDING_SOLID || OUTPUT_PIXEL_DEPTH_OFFSET
	#if INSTANCED_STEREO && SINGLE_PASS_STEREO
		uint TmpEyeIndex = SinglePassStereoIsActive() ? GetEyeIndexFromViewportIndex(InViewportIndex) : GetEyeIndex(FactoryInterpolants.PackedEyeIndex);
		ResolvedView = ResolveView(TmpEyeIndex);
		uint InEyeIndex = SinglePassStereoIsActive() ? TmpEyeIndex : 0; // Only needed with SPS.
	#elif SINGLE_PASS_STEREO
		uint InEyeIndex = GetEyeIndexFromViewportIndex(InViewportIndex);
		ResolvedView = ResolveView(InEyeIndex);
	#elif INSTANCED_STEREO
		ResolvedView = ResolveView(GetEyeIndex(FactoryInterpolants.PackedEyeIndex));
	#else
		ResolvedView = ResolveView();
	#endif

	FMaterialPixelParameters MaterialParameters = GetMaterialPixelParameters(FactoryInterpolants, SvPosition);
	FPixelMaterialInputs PixelMaterialInputs;
	CalcMaterialParameters(MaterialParameters, PixelMaterialInputs, SvPosition, bIsFrontFace);

	#if OUTPUT_PIXEL_DEPTH_OFFSET
	ApplyPixelDepthOffsetToMaterialParameters(MaterialParameters, PixelMaterialInputs, OutDepth);
	#endif

	GetMaterialCoverageAndClipping(MaterialParameters, PixelMaterialInputs);
#endif

	OutColor = 0;
}
